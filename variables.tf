# Deployment Mode
variable "deployment_mode" {
  description = "Deployment mode: 'tenant' for tenancy-wide deployment or 'compartment' for compartment-level deployment"
  type        = string
  default     = "tenant"

  validation {
    condition     = contains(["tenant", "compartment"], var.deployment_mode)
    error_message = "deployment_mode must be either 'tenant' or 'compartment'."
  }
}

# region upwind
variable "resource_suffix" {
  description = "The suffix to append to all resources created by this module."
  type        = string
  default     = ""

  validation {
    condition     = can(regex("^[a-zA-Z0-9]{0,10}$", var.resource_suffix))
    error_message = "The resource suffix must be alphanumeric and cannot exceed 10 characters."
  }
}

variable "upwind_organization_id" {
  description = "The identifier of the Upwind organization to integrate with."
  type        = string

  validation {
    condition     = can(regex("org_[a-zA-Z0-9]{1,}", var.upwind_organization_id))
    error_message = "The Upwind organization ID must start with 'org_' followed by alphanumeric characters."
  }
}

variable "upwind_client_id" {
  description = "The client ID used for authentication with the Upwind Authorization Service."
  type        = string

  validation {
    condition     = can(regex("^[a-zA-Z0-9]{1,}", var.upwind_client_id))
    error_message = "The Upwind client ID must be alphanumeric."
  }
}

variable "upwind_client_secret" {
  description = "The client secret for authentication with the Upwind Authorization Service."
  type        = string
  sensitive   = true
}

variable "scanner_client_id" {
  description = "The client ID used for authentication with the Upwind Cloudscanner Service."
  type        = string
  default     = ""

  validation {
    condition     = var.scanner_client_id == "" || (can(regex("^[a-zA-Z0-9]{1,}", var.scanner_client_id)))
    error_message = "The Upwind scanner client ID must be empty or alphanumeric."
  }
}

variable "scanner_client_secret" {
  description = "The client secret for authentication with the Upwind Cloudscanner Service."
  type        = string
  sensitive   = true
  default     = ""
}

variable "is_dev" {
  description = "Flag to indicate if the environment is a development environment."
  type        = bool
  default     = false
}

variable "upwind_region" {
  description = "The region of the Upwind organization."
  type        = string
  default     = "us"

  validation {
    condition     = var.upwind_region == "us" || var.upwind_region == "eu" || var.upwind_region == "me" || var.upwind_region == "pdc01"
    error_message = "The Upwind region must be one of: us, eu, me, pdc01."
  }
}

# endregion upwind

# region oci

variable "oci_region" {
  description = "The OCI region where resources will be created"
  type        = string
  default     = "us-ashburn-1"
}

variable "oci_tenancy_id" {
  description = "The OCI tenancy ID or compartment ID."
  type        = string

  validation {
    condition     = can(regex("^ocid1\\.(tenancy|compartment)\\..*", var.oci_tenancy_id))
    error_message = "The OCI tenancy ID must be a valid OCI tenancy or compartment OCID."
  }
}

# Orchestrator compartment - different variable names for tenant vs compartment modules
variable "upwind_orchestrator_compartment" {
  description = "The orchestrator compartment where Upwind resources are created. Can be either a compartment OCID or compartment name. Required when deployment_mode is 'tenant'."
  type        = string
  default     = ""
}

variable "upwind_orchestrator_compartment_id" {
  description = "The orchestrator compartment OCID where Upwind resources are created. Used for compartment deployment mode."
  type        = string
  default     = ""

  validation {
    condition     = var.upwind_orchestrator_compartment_id == "" || can(regex("^ocid1\\.compartment\\..*", var.upwind_orchestrator_compartment_id))
    error_message = "The Upwind orchestrator compartment ID must be a valid OCI compartment OCID."
  }
}

# Compartment mode specific
variable "target_compartment_ids" {
  description = "List of compartment IDs to grant access to. Required when deployment_mode is 'compartment'."
  type        = list(string)
  default     = []
}

variable "root_level_compartment_id" {
  description = "The root-level compartment ID for workload identity federation resources. For tenant mode: defaults to tenancy_id. For compartment mode: defaults to orchestrator compartment_id."
  type        = string
  default     = ""

  validation {
    condition     = var.root_level_compartment_id == "" || can(regex("^ocid1\\.(tenancy|compartment)\\..*", var.root_level_compartment_id))
    error_message = "The root_level_compartment_id must be a valid OCI tenancy or compartment OCID."
  }
}

# region workload identity federation
variable "create_identity_domain" {
  description = "Create a new OCI Identity Domain. If false, identity_domain_id must be provided."
  type        = bool
  default     = true
}

variable "identity_domain_id" {
  description = "The OCID of the OCI Identity Domain to use for federation. Required if create_identity_domain is false."
  type        = string
  default     = ""

  validation {
    condition     = var.identity_domain_id == "" || can(regex("^ocid1\\.domain\\..*", var.identity_domain_id))
    error_message = "The Identity Domain ID must be a valid OCI domain OCID (starts with ocid1.domain)."
  }
}

variable "identity_domain_display_name" {
  description = "Display name for the identity domain when creating a new one. Required if create_identity_domain is true."
  type        = string
  default     = ""
}

variable "identity_domain_description" {
  description = "Description for the identity domain when creating a new one."
  type        = string
  default     = "Identity domain for Upwind workload identity federation"
}

variable "identity_domain_license_type" {
  description = "License type for the identity domain. Valid values: 'free', 'premium' for tenant mode, 'DEFAULT', 'PREMIUM', 'STARTER' for compartment mode"
  type        = string
  default     = "free"

  validation {
    condition     = contains(["free", "premium", "DEFAULT", "PREMIUM", "STARTER"], var.identity_domain_license_type)
    error_message = "The identity_domain_license_type must be one of: free, premium, DEFAULT, PREMIUM, STARTER."
  }
}

variable "identity_domain_name" {
  description = "The name of the OCI Identity Domain (e.g., 'Default'). Used in policy statements for domain-scoped groups."
  type        = string
  default     = ""
}

variable "identity_domain_oidc_issuer_url" {
  description = "The OIDC issuer URL for the Identity Domain. If not provided, will be constructed from domain ID."
  type        = string
  default     = ""
}

variable "aws_federated_group_name" {
  description = "The name of the group in the Identity Domain that AWS federated users belong to"
  type        = string
  default     = "aws-federated-workloads"
}
# endregion workload identity federation

variable "enable_cloudscanners" {
  description = "Enable the creation of cloud scanners."
  type        = bool
  default     = false
}

variable "enable_dspm_scanning" {
  description = "Enable DSPM scanning by cloud scanners"
  type        = bool
  default     = false
}

variable "oci_vault_id" {
  description = "Optional OCID of an existing OCI Vault to use for storing secrets. If not provided, a new vault will be created."
  type        = string
  default     = ""

  validation {
    condition     = var.oci_vault_id == "" || can(regex("^ocid1\\.vault\\..*", var.oci_vault_id))
    error_message = "The OCI vault ID must be a valid OCI vault OCID (starts with ocid1.vault)."
  }
}

variable "oci_vault_key_id" {
  description = "Optional OCID of an existing OCI Vault Key to use for encrypting secrets. If not provided and oci_vault_id is provided, a new key will be created in the existing vault."
  type        = string
  default     = ""

  validation {
    condition     = var.oci_vault_key_id == "" || can(regex("^ocid1\\.key\\..*", var.oci_vault_key_id))
    error_message = "The OCI vault key ID must be a valid OCI key OCID (starts with ocid1.key)."
  }
}

# endregion oci

variable "tags" {
  description = "A map of tags to apply to all resources"
  type        = map(string)
  default     = {}
}

